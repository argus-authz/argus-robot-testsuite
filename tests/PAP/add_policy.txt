*** Settings ***
Library    OperatingSystem
Resource   lib/utils.txt
Resource   variables.txt

Suite Setup     Make backup of the configuration
Suite Teardown  Restore configurations

Test Setup     Start PAP service
Test Teardown  Clean up


*** Test Cases ***
Add policy from file
  ${policy}=  Prepare
  Create policy file  ${policy}
  Load policy file

Add policy from file with error
  ${policy}=  Prepare with error
  Create policy file  ${policy}
  Execute and Check Failure  ${PAP_ADMIN} apf ${POLICY_FILE}

DN containing slash (bug 66669)
  Remove all policies
  ${pol}=  catenate  SEPARATOR=
  ...  resource "resource_1" { \n
  ...    action ".*" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name/testslash" } \n
  ...    } \n
  ...  } \n
  Create policy file  ${pol}
  Load policy file
  Remove policy file
  ${pol}=  catenate  SEPARATOR=
  ...  resource "resource_1" { \n
  ...    action ".*" { \n
  ...      rule permit { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/slashtest/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  } \n
  Create policy file  ${pol}
  Load policy file
  Execute and Check Success  ${PAP_ADMIN} lp --resource ${TEST_RESOURCE}
  

*** Keywords ***
Prepare
  ${policies}=  catenate  SEPARATOR=
  ...  resource ".*" { \n
  ...    action ".*" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  } \n
  ...  resource ".*" { \n
  ...    action ".*" { \n
  ...      rule deny { fqan="/badvo" } \n
  ...    } \n
  ...  } \n
  [Return]  ${policies}

Prepare with error
  ${policies}=  catenate  SEPARATOR=
  ...  resource ".*" { \n
  ...    action ".*" { \n
  ...      rule deni { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  } \n
  [Return]  ${policies}