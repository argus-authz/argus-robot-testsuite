*** Settings ***
Library    OperatingSystem
Resource   lib/utils.txt
Resource   variables.txt

Suite Setup     Make backup of the configuration
Suite Teardown  Restore configurations

Test Teardown  Clean up

*** Test Cases ***

Remove all policies
  [Setup]  Prepare
  Execute and Check Success  ${PAP_ADMIN} rap
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -sai | egrep -c 'id='
  Should Be Equal As Integers  ${output}  0

Remove with non existing id
  Execute and Check Failure  ${PAP_ADMIN} rp ${DUMMY_ID}

Remove with resource id
  [Setup]  Prepare
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -srai | egrep -m 1 'id=' | awk '{print $1}' | sed 's/id=//'
  Execute and Check Success  ${PAP_ADMIN} rp ${output}
  
Remove with action id
  [Setup]  Prepare test 3
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -srai | egrep 'id=' | tail -1 | awk '{print $1}' | sed 's/id=//'
  Execute and Check Success  ${PAP_ADMIN} rp ${output}
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -srai | egrep -c 'id='
  Should Be Equal As Integers  ${rc}  0
  Should Be Equal As Integers  ${output}  2
  
Remove with rule id
  [Setup]  Prepare test 4
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -sai | egrep 'id=' | tail -1 | awk '{print $1}' | sed 's/id=//' | xargs
  Execute and Check Success  ${PAP_ADMIN} rp ${output}
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -sai | egrep -c 'id='
  Should Be Equal As Integers  ${rc}  0
  Should Be Equal As Integers  ${output}  3

Remove with multiple rules
  [Setup]  Prepare test 5
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -sai | egrep 'id=' | tail -4 | head -3 | awk '{print $1}' | sed 's/id=//' | xargs
  Execute and Check Success  ${PAP_ADMIN} rp ${output}
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -sai | egrep -c 'id='
  Should Be Equal As Integers  ${rc}  0
  Should Be Equal As Integers  ${output}  3
  
Remove with multiple rules and one wrong
  [Setup]  Prepare test 5
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -sai | egrep 'id=' | tail -4 | head -3 | awk '{print $1}' | sed 's/id=//' | xargs
  Execute and Check Failure  ${PAP_ADMIN} rp ${output} ${DUMMY_ID}
  ${rc}  ${output}=  Run And Return Rc And Output  ${PAP_ADMIN} lp -sai | egrep -c 'id='
  Should Be Equal As Integers  ${rc}  0
  Should Be Equal As Integers  ${output}  3
  
Remove with empty repository
  [Setup]  Remove all policies
  Execute and Check Failure  ${PAP_ADMIN} rp ${DUMMY_ID}


*** Keywords ***

Prepare
  ${policies}=  catenate  SEPARATOR=
  ...  resource "resource_1" { \n
  ...    action ".*" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  } \n
  ...  resource "resource_2" { \n
  ...    action ".*" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  } \n
  ...  resource "resource_3" { \n
  ...    action ".*" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  } \n
  Prepare PAP environment  ${policies}

Prepare test 3
  ${policies}=  catenate  SEPARATOR=
  ...  resource "resource_1" { \n
  ...    action "submit-job" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  \n
  ...    action "get-status" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...    } \n
  ...  } \n
  Prepare PAP environment  ${policies}

Prepare test 4
  ${policies}=  catenate  SEPARATOR=
  ...  resource "resource_1" { \n
  ...    action "get-status" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999998/CN=user name 2" } \n
  ...    } \n
  ...  } \n
  Prepare PAP environment  ${policies}

Prepare test 5
  ${policies}=  catenate  SEPARATOR=
  ...  resource "resource_1" { \n
  ...    action "get-status" { \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999999/CN=user name" } \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999998/CN=user name 2" } \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999997/CN=user name 3" } \n
  ...      rule deny { subject="/DC=ch/DC=cern/OU=Organic Units/OU=Users/CN=user/CN=999998/CN=user name 2" } \n
  ...    } \n
  ...  } \n
  Prepare PAP environment  ${policies}