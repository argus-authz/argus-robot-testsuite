*** Settings ***
Resource   lib/utils.txt

Suite Setup     Make backup of the configuration
Suite Teardown  Restore configurations


*** Test Cases ***
User group mapping (bug 64340)
  &{dict}=  Create Dictionary  vo_map=no  dn_map=yes
  ...  grp_vo_map=no  grp_vo_sec_map=no  grp_dn_map=yes
  ...  pref_dn_for_login=true  pref_dn_for_primary_grp=true  no_primary_grp_is_error=true
  Prepare PEP environment  &{dict}
  ${output}=  Perform PEP request  ${USERKEY}  ${USERCERT}  ${USERCERT}  ${TEST_RESOURCE}  ${TEST_ACTION}  ${VO}
  Should Contain  ${output}  ${TEST_RESOURCE}  Did not find expected resource: ${TEST_RESOURCE}
  Should Contain Ignore Case  ${output}  ${TEST_RULE}  Did not find expected rule: ${TEST_RULE}
  ${username}=  Get match  ${output}  Username: (.*)
  Should Not Be Empty  ${username}  No user account mapped.
  ${group}=  Get match  ${output}  Group: (.*)
  Should Not Be Empty  ${group}  No user group mapped.
  ${sec_group}=  Get match  ${output}  Secondary Groups: (.*)
  Should Not Be Empty  ${sec_group}  No user secondary group mapped.
  Should Be Equal  ${group}  ${sec_group}
  [Teardown]  Restore PEP configuration

DN group mapping (bug 68805)
  ${host_dn}=  Get host dn
  ${str}=  Escape char  ${host_dn}  /
  Replace string  ${GRIDDIR}/${VOMSGRIDMAPFILE}  "${str}"  \# Ignore
  ${file}=  Set Variable  ${GRIDDIR}/${GRIDMAPFILE}
  Empty file  ${file}
  Append To File  ${file}  \n"${host_dn}" ${TEST_DN_UID}
  ${file}=  Set Variable  ${GRIDDIR}/${GROUPMAPFILE}
  Empty file  ${file}
  Append To File  ${file}  \n"${host_dn}" ${TEST_DN_UID_GROUP}
  Restart PEP service
  Start PDP service
  Start PAP service
  Remove all policies
  ${action}=  Set Variable  do_not_test
  Add policy with obligation  ${TEST_RESOURCE}  ${TEST_ACTION}  ${TEST_OBLIGATION}  ${TEST_RULE}  ${host_dn}
  Add policy  ${TEST_RESOURCE}  ${action}  ${TEST_RULE}  ${host_dn}
  Reload policy
  ${output}=  Perform PEP request  ${HOSTKEY}  ${HOSTCERT}  ${HOSTCERT}  ${TEST_RESOURCE}  ${TEST_ACTION}  None
  Should Contain  ${output}  ${TEST_RESOURCE}
  Should Contain Ignore Case  ${output}  ${TEST_RULE}
  ${username}=  Get match  ${output}  Username: (.*)
  Should Not Be Empty  ${username}  No user account mapped.
  ${group}=  Get match  ${output}  Group: (.*)
  Should Not Be Empty  ${group}  No user group mapped.
  ${sec_group}=  Get match  ${output}  Secondary Groups: (.*)
  Should Not Be Empty  ${sec_group}  No user secondary group mapped.
  [Teardown]  Restore PEP configuration